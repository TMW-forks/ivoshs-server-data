//valentine 2010
008-1.gat,38,22,0	script	Cupid	189, {

L_Start:
	mes "[Cupid]";
        mes "\"Hello there, I'm Cupid, and Who are you?\"";
        next;
        input @name$;
	if(BaseLevel <= 29) goto L_Young;
	if(Valen2kx == 2) goto L_Welcome;
	if(Valen2kx == 3) goto L_Bad;
	if(Valen2kx == 4) goto L_Back;
	if(Valen2kx == 5) goto L_Done;
	if(Valen2kx == 99) goto L_Back;
	if(Valen2kx >= 10) goto L_Back;
	mes "[Cupid]";
        mes "\"Hello, " + @name$ + "! Are you ready for Valentines Day? Have you found your sweetheart?\"";
        next;
	menu 
		"Yes I have!", L_StartYes,
		"No, I have not found the right person yet!", L_StartNo;

L_StartYes:
	mes "[Cupid]";
	mes "\"Oh I am so glad to hear that. This is the best time of the year.";
	mes "Its important to share with someone.\"";
	next;
	mes "[Cupid]";
	mes "\"Take these, and give them to your loved one.\"";
	getitem 661,12;
	getitem 737,1;
	next;
	menu
		"Can I ask a question?", L_Glasses,
		"Thank you very much!", L_Maybe;

L_StartNo:
	mes "[Cupid]";
	mes "\"Oh I am sorry to hear that. This time of year you need someone special.";
	mes "We must hurry!\"";
	next;
	mes "[Cupid]";
	mes "\"Take these,and when you find that special someone, you can share.\"";
	getitem 661,12;
	getitem 737,1;
	next;
	menu
		"Can I ask a question?", L_Glasses,
		"Thank you very much!", L_Maybe;

L_Glasses:
	mes "[Cupid]";
	mes "\"Only if you are sure that you can handle the answer!\"";
	menu
		"Ummm, maybe not, nevermind.", L_Maybe,
		"Why, do you wear those thick glasses?", L_Explain;
	close;
        
L_Explain:
	mes "[Cupid]";
	mes "\"Well without the glasses I am Blind!";
	mes "Ever heard the saying Love is Blind?";
	mes "Thats where the saying came from!\"";
	next;
	mes "[Cupid]";
	mes "\"Seriously that was your question?, My turn to ask you one.";
	mes "Would you be interested in giving me some help?\"";
	menu
		"Sure, I have some extra time to lend a hand.", L_Quest,
		"That would depend on what you need.", L_Maybe,
		"Do I look like a cub scout?", L_BadAtt;

L_BadAtt:
	mes "[Cupid]";
	mes "\"Well thats a bad attitude, you dont deserve those gifts I gave you\"";
	set Valen2kx, 3;
	delitem 661,12;
	delitem 737,1;
	close;
	
L_Maybe:
	mes "[Cupid]";
	mes "\"These glasses are a pain, and the whole blindness thing is a problem.";
	mes "I use twice as many arrows as I need and I keep missing the target.";
	mes "I need more enchanted arrows and havent the time to get them.\"";
	next;
	mes "[Cupid]";
	mes "\"Interested in helping?\"";
	set Valen2kx, 2;
	menu
		"Sure, I think I can handle that for you.", L_Quest,
		"I dont think I have the time for this now but maybe later.", L_NotNow,
		"Do I look like a cub scout?", L_BadAtt;	

L_Welcome:
	mes "[Cupid]";
        mes "\"Hello, " + @name$ + "! Welcome Back\"";
        next;
	mes "Have time now to help?";
	menu
		"Sure, I think I have time now to help you.", L_Quest,
		"No, sorry!", L_NotNow;

L_NotNow:	
	mes "[Cupid]";
	mes "\"Very well";
	mes "If you change your mind, please come see me.\"";
	close;

L_Bad:
	mes "[Cupid]";
        mes "\"Oh its you, " + @name$ + "! I remember you, you were mean to me earlier!\"";
        next;
	menu 
		"I am sorry! Can I ask you a question?", L_Glasses,
		"I was wrong, I am sorry. I would like to help,", L_Maybe;


L_Quest:
	mes "[Cupid]";
	mes "\"I am so glad you have agreed to help!";
	mes "There is only one person I know that makes enchanted arrows in this Realm";
	mes "And he moves around alot.\"";
	next;
	mes "[Cupid]";
	mes "\"Last I heard, he was collecting furs and food";
	mes "Something about needing a cold place to build the right type of forge.";
	mes "Look for Herzensbrecher, he is the one we need.\"";
	next;
	mes "[Cupid]";
	mes "\"Tell him that I sent you, and that it is very urgent.";
	mes "I need a batch of his Enchanted Arrows\"";
	next;
	mes "[Cupid]";
	mes "Bring them back, I will reward you";
	set Valen2kx, 4;
	close;

L_Back:
	mes "[Cupid]";
	mes "\"Oh its you, " + @name$ + "! I remember you, did you find Herzensbrecher?\"";
        menu
		"Yes, I have what you asked for.", L_Finish,
		"No, I am still collecting supplies.", L_Hurry;

L_Hurry:
	mes "[Cupid]";
	mes "Oh please hurry, I am getting dangerously low on arrows.";
	close;

L_Finish:
	if(countitem(1243) <1000) goto L_NotYet;
        delitem 1243, 1000;
        mes "[Cupid]";
        mes "Thank you for your help.";
	mes "Take these as a token of my gratitude. These should always make you lucky in love.";
	getitem 1249, 1;
	getexp 200000, 0;
	set zeny, zeny + 100000;
	set Valen2kx, 5;
        close;

L_NotYet:
	mes "[Cupid]";
	mes "\"Were you robbed?";
	mes "You dont have any.";
	mes "Come back when you have a the full set of arrows.\"";
	close;

L_Done:
	mes "[Cupid]";
	mes "Thank you so much for your help.";
	mes "These New arrows are flying Straight and True.";
	close;

L_Young:
	mes "[Cupid]";
	mes "\"Hello, " + @name$ + "! Its a lovely time of year.";
	mes "When you are older you will enjoy Love.";
	mes "Now run along, Cupid has work to do.\"";
	close;
	
}


       
